/* uSysML -- textX grammar to mimic a small subset of SysML v2
*/

uSysML:
    types*=uSysMLType
    elements*=Element
;

uSysMLType:
    'type' name=Name
;

Element:
    Package | PartDef | Part | AttributeDef | Attribute | RedefinedPart
;

OwnedElement:
    Package | PartDef | Part | AttributeDef | Attribute | RedefinedPart
;

Package:
   'package' name=Name (
    ';' | ( '{' elements*=OwnedElement '}' )
    )
;

PartDef:
    'part' 'def' name=Name (
    ';' | ( '{' elements*=OwnedElement '}' )
    )
;

// ? means 'optional'
Part:
    'part' name=Name
        ( ':' type=[PartDef|Name|^elements] )? mult=Multiplicity?
        (';' | '{' elements*=OwnedElement '}' )
;

Multiplicity:
    '[' lower_bound=INT ('..' upper_bound=INT)? ']'
;

RedefinedPart:
    'part' 'redefines' type=[Part|Name|^elements] ';'
;

AttributeDef:
    'attribute' 'def' name=Name (
    ';' | ( '{' elements*=OwnedAttributeElement '}' )
    )
;

OwnedAttributeElement:
    Attribute
;

Attribute:
    'attribute' name=Name ':' ( type=[uSysMLType] | type=[AttributeDef|Name|^elements] ) ';'
;

Name:
    BaseName | UnrestrictedName
;

BaseName: !Keyword ID;
Keyword: /\b(package|type|attribute|part|def|redefines)\b/;

UnrestrictedName[noskipws]: /\s*'/- /(\\'|\\"|\\b|\\f|\\t|\\n|\\\\|[^'])+/ '\''-;

Comment:
    /\/\/.*$/
;
