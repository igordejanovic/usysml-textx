/* uSysML -- textX grammar to mimic a small subset of SysML v2
*/

uSysML:
    types*=uSysMLType
    elements*=Element
;

uSysMLType:
    'type' name=Name
;

Element:
    Package | PartDef | PartUsage | AttributeDef | AttributeUsage | RedefinedPart
;

OwnedElement:
    Package | PartDef | PartUsage | AttributeDef | AttributeUsage | RedefinedPart
;

Package:
   'package' name=Name (
    ';' | ( '{' elements*=OwnedElement '}' )
    )
;

PartDef:
    'part' 'def' name=Name (
    ';' | ( '{' elements*=OwnedElement '}' )
    )
;

// ? means 'optional'
PartUsage:
    'part' name=Name
        ( ':' type=[PartDef|Name|^elements] )? mult=Multiplicity?
        (';' | '{' elements*=OwnedElement '}' )
;

Multiplicity:
    '[' lower_bound=INT ('..' upper_bound=INT)? ']'
;

RedefinedPart:
    'part' 'redefines' type=[PartUsage|Name|^elements] ';'
;

AttributeDef:
    'attribute' 'def' name=Name (
    ';' | ( '{' elements*=OwnedAttributeElement '}' )
    )
;

OwnedAttributeElement:
    AttributeUsage
;

AttributeUsage:
    'attribute' name=Name ':' ( type=[AttributeType|Name|^elements] ) ';'
;

AttributeType:
    uSysMLType | AttributeDef;

Name:
    BaseName | UnrestrictedName
;

BaseName: !Keyword ID;
Keyword: /\b(package|type|attribute|part|def|redefines)\b/;

UnrestrictedName[noskipws]: /\s*'/- /(\\'|\\"|\\b|\\f|\\t|\\n|\\\\|[^'])+/ /'/-;

Comment:
    /\/\/.*$/
;
